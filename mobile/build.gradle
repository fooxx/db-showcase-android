apply plugin: 'com.android.application'

apply plugin: 'realm-android'//REALM.IO

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Last Git tag name on current branch
final GIT_TAG_NAME = 'git describe --tags --abbrev=0'.execute([], project.rootDir).text.trim()
// Total number of tags in the app
final GIT_TAG_COUNT = 'git tag --list'.execute([], project.rootDir).text.split("\n").size()

apply from: 'common.gradle'

repositories {
	flatDir {
		dirs 'libs'
	}
	mavenCentral()
}
android {
	compileSdkVersion compile_sdk_version
	buildToolsVersion build_tools_version

	dataBinding.enabled = true

	defaultConfig
			{
				applicationId "cz.koto.misak.dbshowcase.android"
				minSdkVersion min_sdk_version
				targetSdkVersion compile_sdk_version
				versionCode GIT_TAG_COUNT
				versionName GIT_TAG_NAME
				aaptOptions.cruncherEnabled false
				multiDexEnabled true
				resValue "string", "app_name", "DBShowcase"
				testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
				vectorDrawables.useSupportLibrary = true
			}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		main.java.srcDirs += 'src/debug/kotlin'
		server.java.srcDirs += 'src/server/kotlin'
		mock.java.srcDirs += 'src/mock/kotlin'
	}

	lintOptions {
		abortOnError false
	}


	flavorDimensions "env"
	productFlavors {
		mock {
			dimension = "env"
			applicationIdSuffix ".mock"
			versionNameSuffix "-mock"
			resValue "string", "app_name", "DBShowMock"
			buildConfigField "String", "REST_BASE_URL", "\"http://dummy.com/\""
		}

		server {
			dimension = "env"
			resValue "string", "app_name", "DBShow"
			buildConfigField "String", "REST_BASE_URL", "\"https://kotopeky.cz/\""
		}
	}



	buildTypes {

		debug {
			buildConfigField "boolean", "LOGS", "true"
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			aaptOptions.cruncherEnabled true
			// Disable fabric build ID generation for debug builds
			ext.enableCrashlytics = false
			// Only use this flag on builds you don't proguard or upload to beta-by-crashlytics
			ext.alwaysUpdateBuildId = false
		}

		release {
			buildConfigField "boolean", "LOGS", "false"

			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled true
			shrinkResources true

			proguardFile 'proguard-rules/proguard-square-retrofit2.pro'
			proguardFile 'proguard-rules/proguard-square-leakcanary.pro'
			proguardFile 'proguard-rules/proguard-square-okio.pro'
			proguardFile 'proguard-rules/proguard-glide.pro'
			proguardFile 'proguard-rules/proguard-gson.pro'
			proguardFile 'proguard-rules/proguard-square-okhttp3.pro'
			proguardFile 'proguard-rules/proguard-support-design.pro'
			proguardFile 'proguard-rules/proguard-support-v7-appcompat.pro'
			proguardFile 'proguard-rules/proguard-support-v7-cardview.pro'
			proguardFile 'proguard-rules/proguard-realm.pro'
			proguardFile 'proguard-rules/proguard-reactive.pro'
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
}


dependencies
		{
			compile 'com.android.support:multidex:1.0.2'

			compile('cz.koto:android-keystore-compat:1.2.0') {
				exclude group: 'com.android.support'
				exclude group: 'org.jetbrains.kotlin'
			}

			kapt "com.android.databinding:compiler:$android_gradle_version"
			implementation "com.android.databinding:library:$android_binding_version"
			implementation "com.android.databinding:adapters:$android_binding_version"

			compile "android.arch.lifecycle:extensions:$rootProject.archLifecycleVersion"
			annotationProcessor "android.arch.lifecycle:compiler:$rootProject.archLifecycleVersion"

			compile "com.android.support:appcompat-v7:$supportLibraryVersion"
			compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
			compile "com.android.support:cardview-v7:$supportLibraryVersion"
			compile "com.android.support:design:$supportLibraryVersion"
			compile "com.android.support:palette-v7:$supportLibraryVersion"
			compile "com.android.support:percent:$supportLibraryVersion"
			compile "com.android.support:support-annotations:$supportLibraryVersion"
			compile "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

			compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
			compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
			compile "com.squareup.okhttp3:logging-interceptor:$okhttpLoggingInterceptorVersion"

			compile "io.reactivex.rxjava2:rxjava:$rxjavaversion"
			compile "io.reactivex.rxjava2:rxandroid:$rxjavaversion"

			compile "com.jakewharton.timber:timber:$timberVersion"

			compile "org.parceler:parceler-api:$parcelerVersion"
			annotationProcessor "org.parceler:parceler:$parcelerVersion"

			annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
			compile "com.google.dagger:dagger:$daggerVersion"
			provided "javax.annotation:jsr250-api:1.0"

			compile "cz.kinst.jakub:android-stateful-layout-simple:$androidStatefulLayoutSimpleVersion"
			compile "cz.kinst.jakub:viewmodelbinding:$kinstViewModelBindingVersion"
			compile "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter:$tatarkaVersion"
			compile "me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:$tatarkaVersion"

			compile "com.facebook.stetho:stetho:$stethoVersion"
			compile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"
			compile "com.facebook.stetho:stetho-urlconnection:$stethoVersion"
			compile "com.uphyca:stetho_realm:$stethoRealmVersion"

			compile "com.getbase:floatingactionbutton:$floatingActionButtonVersion"

			annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:$dbflowVersion"
			compile "com.github.Raizlabs.DBFlow:dbflow-core:$dbflowVersion"
			compile "com.github.Raizlabs.DBFlow:dbflow:$dbflowVersion"
			compile "com.github.Raizlabs.DBFlow:dbflow-sqlcipher:$dbflowVersion"
			compile "com.github.Raizlabs.DBFlow:dbflow-rx2:$dbflowVersion"
			compile "net.zetetic:android-database-sqlcipher:$sqlCipherVersion@aar"
			compile "com.github.Raizlabs.DBFlow:dbflow-rx2:$sqlCipherVersion"

			debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
			releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
			testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

			compile fileTree(include: ['*.jar'], dir: 'libs')
			compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
		}